{ config, lib, ... }:

let
  domain = "dora.im";
  netbirdDomain = "ts.${domain}";
  clientId = "netbird";
in
{
  services.netbird.server = {
    enable = true;
    enableNginx = true;
    domain = netbirdDomain;

    coturn = {
      enable = true;
      domain = netbirdDomain;
      passwordFile = config.sops.secrets."netbird/Password".path;
      openPorts = with config.services.coturn; [
        listening-port
        tls-listening-port
      ];
    };

    signal = {
      enable = true;
      enableNginx = true;
      domain = netbirdDomain;
    };

    dashboard = {
      enable = true;
      enableNginx = true;
      domain = netbirdDomain;
      settings = {
        AUTH_AUTHORITY = "https://sso.${domain}/application/o/netbird/";
        AUTH_CLIENT_ID = clientId;
        AUTH_AUDIENCE = clientId;
      };
    };

    management = {
      enable = true;
      enableNginx = true;
      domain = netbirdDomain;
      turnDomain = netbirdDomain;
      singleAccountModeDomain = netbirdDomain;
      oidcConfigEndpoint = "https://sso.${domain}/application/o/netbird/.well-known/openid-configuration";

      settings = {
        Signal.URI = "${netbirdDomain}:443";

        HttpConfig.AuthAudience = clientId;
        IdpManagerConfig.ClientConfig.ClientID = clientId;
        DeviceAuthorizationFlow.ProviderConfig = {
          Audience = clientId;
          ClientID = clientId;
        };
        PKCEAuthorizationFlow.ProviderConfig = {
          Audience = clientId;
          ClientID = clientId;
        };

        TURNConfig = {
          Secret._secret = config.sops.secrets."netbird/Password".path;
          CredentialsTTL = "12h";
          TimeBasedCredentials = false;
          Turns = [
            {
              Password._secret = config.sops.secrets."netbird/Password".path;
              Proto = "udp";
              URI = "turn:${netbirdDomain}:3478";
              Username = "netbird";
            }
          ];
        };
        Relay = {
          Addresses = [ "rels://${netbirdDomain}:33080" ];
          CredentialsTTL = "24h";
          Secret._secret = config.sops.secrets."netbird/Password".path;
        };
        DataStoreEncryptionKey._secret = config.sops.secrets."netbird/Password".path;
      };
    };
  };

  # Make the env available to the systemd service
  systemd.services.netbird-management.serviceConfig = {
    EnvironmentFile = config.sops.templates."netbird-env".path;
  };
  sops.templates."netbird-env".content = ''
    # OIDC 配置端点
    NETBIRD_AUTH_OIDC_CONFIGURATION_ENDPOINT="https://sso.dora.im/application/o/netbird/.well-known/openid-configuration"
    NETBIRD_USE_AUTH0=false

    # Client / Audience
    NETBIRD_AUTH_CLIENT_ID="netbird"
    NETBIRD_AUTH_SUPPORTED_SCOPES="openid profile email offline_access api"
    NETBIRD_AUTH_AUDIENCE="netbird"

    # 设备授权
    NETBIRD_AUTH_DEVICE_AUTH_CLIENT_ID="netbird"
    NETBIRD_AUTH_DEVICE_AUTH_AUDIENCE="netbird"

    # 回调
    NETBIRD_AUTH_REDIRECT_URI="/auth"
    NETBIRD_AUTH_SILENT_REDIRECT_URI="/silent-auth"

    # 管理端对接 Authentik
    NETBIRD_MGMT_IDP="authentik"
    NETBIRD_IDP_MGMT_CLIENT_ID="netbird"
    NETBIRD_IDP_MGMT_EXTRA_USERNAME="Netbird"
    NETBIRD_IDP_MGMT_EXTRA_PASSWORD="eA7DvG2ZwWFiOqxXblBgRO1s8SWa45rC5GcpZA3aCxo3d1hyW4M1mfNSxYpaezrhp2vu1u07PvzdYxhRY4ijTOCq5cY3rdG2Q9pMxEGAvnINyoBgVbNYLVnCgHmM7pQT"

    # 已知问题：禁用 PKCE 的 login 提示（参考 issue #3654）
    NETBIRD_AUTH_PKCE_DISABLE_PROMPT_LOGIN=true
  '';

  # Override ACME settings to get a cert
  # services.nginx.virtualHosts = lib.mkMerge [
  #   {
  #     "${netbirdDomain}" = {
  #       enableACME = true;
  #       forceSSL = true;
  #     };
  #   }
  # ];
  sops.secrets = {
    "netbird/Password" = { };
  };

  # Run the Netbird relay with TLS to allow relaying over TCP
  virtualisation.oci-containers.containers.netbird-relay = {
    image = "netbirdio/relay:latest";
    ports = [
      "33080:33080"
    ];
    volumes = [
      "/var/lib/traefik-certs/${netbirdDomain}/:/certs:ro"
    ];
    environment = {
      NB_LOG_LEVEL = "info";
      NB_LISTEN_ADDRESS = ":33080";
      NB_EXPOSED_ADDRESS = "rels://${netbirdDomain}:33080";
      NB_TLS_CERT_FILE = "/certs/certificate.crt";
      NB_TLS_KEY_FILE = "/certs/privatekey.key";
    };
    # environmentFiles = [
    #   "/path/to/netbird/relay_secret_container"
    # ];
  };
  services.traefik.dynamicConfigOptions.http = {
    routers = {
      netbird = {
        rule = "Host(`${netbirdDomain}`)";
        entryPoints = [ "https" ];
        service = "netbird";
      };
    };
    services = {
      netbird.loadBalancer = {
        passHostHeader = true;
        servers = [ { url = "http://${netbirdDomain}:${toString config.ports.nginx}"; } ];
      };
    };
  };

  networking.firewall.allowedTCPPorts = [
    80
    443
    3478
    10000
    33080
  ];
  networking.firewall.allowedUDPPorts = [
    3478
    5349
    33080
  ];
  networking.firewall.allowedUDPPortRanges = [
    {
      from = 40000;
      to = 40050;
    }
  ]; # TURN ports
}
